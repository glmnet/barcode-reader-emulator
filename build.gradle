plugins {
    id 'java'
    id 'org.beryx.runtime' version '1.11.4'
    id 'edu.sc.seis.launch4j' version '2.5.2'
    id "org.sonarqube" version "3.3"
    id "jacoco"
}

sourceCompatibility = '11'
targetCompatibility = '11'

group 'oxcafedead'
version '0.4.1'

runtime {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    modules = ['java.desktop']
    imageDir = file("${buildDir}/launch4j/runtime")
}

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    implementation group: 'net.java.dev.jna', name: 'jna', version: '4.5.0'
    implementation group: 'org.apache.camel', name: 'camel-barcode', version: '2.21.1'
    testImplementation group: 'junit', name: 'junit', version: '4.12'
    testImplementation 'org.mockito:mockito-core:1.9.5'
}

createExe {
    mainClassName = 'oxcafedead.barcodereader.BarcodeReaderEmulator'
    icon = "${projectDir}/app.ico"
    jreMinVersion = "11"
    jar = "${buildDir}/libs/${project.name}-${project.version}.jar"
    restartOnCrash = false
    downloadUrl = "https://adoptium.net/?variant=openjdk17"
}

task zipExecutableWithRuntime(type: Zip) {
    tasks.createExe.configure {
        bundledJrePath = null
        bundledJre64Bit = false
        bundledJreAsFallback = false
    }
    dependsOn tasks.runtime
    dependsOn tasks.createExe
    from "${buildDir}/launch4j/"
    include '*'
    include '**/*'
    archiveName "emulator.zip"
    destinationDir(file("${buildDir}/distr"))
}

task zipExecutable(type: Zip) {
    tasks.createExe.configure {
        bundledJrePath = 'runtime'
        bundledJre64Bit = true
        bundledJreAsFallback = true
    }
    dependsOn tasks.createExe
    from "${buildDir}/launch4j/"
    include '*'
    include '**/*'
    archiveName "emulator-no-runtime.zip"
    destinationDir(file("${buildDir}/distr-no-runtime"))
}

jacoco {
    toolVersion = "0.8.7"
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
        csv.enabled false
    }

    dependsOn test
}

sonarqube {
    properties {
        property "sonar.projectKey", "oxcafedead_barcode-reader-emulator"
        property "sonar.organization", "oxcafedead"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

project.tasks["sonarqube"].dependsOn ":jacocoTestReport"
