plugins {
    id 'java'
    id 'org.beryx.runtime' version '1.11.4'
    id 'edu.sc.seis.launch4j' version '2.4.6'
}

runtime {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    modules = ['java.se', 'java.desktop', 'java.logging']
    imageDir = file("${buildDir}/launch4j/runtime")
}

group 'oxcafedead'
version '0.4.0-SNAPSHOT'

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile group: 'net.java.dev.jna', name: 'jna', version: '4.5.0'
    compile group: 'org.apache.camel', name: 'camel-barcode', version: '2.21.1'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

launch4j {
    mainClassName = 'oxcafedead.barcodereader.BarcodeReaderEmulator'
    icon = "${projectDir}/app.ico"
    bundledJrePath = 'runtime'
    bundledJre64Bit = true
    jar = "${buildDir}/libs/${project.name}-${project.version}.jar"
    restartOnCrash = false
}

task zipExecutableWithRuntime(type: Zip) {
    dependsOn tasks.runtime
    dependsOn tasks.createExe
    from "${buildDir}/launch4j/"
    include '*'
    include '**/*'
    archiveName "emulator.zip"
    destinationDir(file("${buildDir}/distr"))
}