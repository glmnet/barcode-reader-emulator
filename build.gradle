plugins {
    id 'java'
    id 'org.beryx.runtime' version '1.11.4'
    id 'edu.sc.seis.launch4j' version '2.4.6'
    id "org.sonarqube" version "3.3"
    id "jacoco"
}

runtime {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    modules = ['java.se', 'java.desktop', 'java.logging']
    imageDir = file("${buildDir}/launch4j/runtime")
}

group 'oxcafedead'
version '0.4.1'

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    implementation group: 'net.java.dev.jna', name: 'jna', version: '4.5.0'
    implementation group: 'org.apache.camel', name: 'camel-barcode', version: '2.21.1'
    testImplementation group: 'junit', name: 'junit', version: '4.12'
    testImplementation 'org.mockito:mockito-core:1.9.5'
}

launch4j {
    mainClassName = 'oxcafedead.barcodereader.BarcodeReaderEmulator'
    icon = "${projectDir}/app.ico"
    bundledJrePath = 'runtime'
    bundledJre64Bit = true
    jar = "${buildDir}/libs/${project.name}-${project.version}.jar"
    restartOnCrash = false
}

task zipExecutableWithRuntime(type: Zip) {
    dependsOn tasks.runtime
    dependsOn tasks.createExe
    from "${buildDir}/launch4j/"
    include '*'
    include '**/*'
    archiveName "emulator.zip"
    destinationDir(file("${buildDir}/distr"))
}

jacoco {
    toolVersion = "0.8.7"
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
        csv.enabled false
    }

    dependsOn test
}

sonarqube {
    properties {
        property "sonar.projectKey", "oxcafedead_barcode-reader-emulator"
        property "sonar.organization", "oxcafedead"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

project.tasks["sonarqube"].dependsOn ":jacocoTestReport"
